apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'shadow'

// Use Java 7 by default
// sourceCompatibility = '1.7'
// targetCompatibility = '1.7'

// UTF-8 should be standard by now. So use it!
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

// The main class of the application
mainClassName = 'com.example.helloworld.HelloWorldService'

// Add Gradle Shadow plugin, which is akin to Maven Shade
// https://github.com/johnrengelman/shadow
buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:0.8'
    }
}

// Set our project variables
project.ext {
    dropwizardVersion = '0.6.2'
}

repositories {
    mavenCentral()
}

dependencies {
    compile (
       'com.yammer.dropwizard:dropwizard-core:' + dropwizardVersion,
       'com.yammer.dropwizard:dropwizard-hibernate:' + dropwizardVersion,
       'com.yammer.dropwizard:dropwizard-migrations:' + dropwizardVersion,
       'com.yammer.dropwizard:dropwizard-auth:' + dropwizardVersion,
    )
}

// Configure the shadowJar task
version = '0.1'

// The Shadow plugin repackages classes, which breaks classes which
// have an attached signature.  So exclude the signatures.
shadow {
//    exclude 'META-INF/*.SF'
    exclude 'META-INF/*.DSA'
    exclude 'META-INF/*.RSA'
}
jar {
    manifest {
        attributes 'Main-Class': mainClassName
    }
}


// Configure the run task with the correct args for the Dropwizard service
run {
    args 'server', './src/dist/config/helloworld.yml'
}

/*
 * gradle wrapper
 * 
 * Running this task will create a Gradle wrapper script, named
 * gradlew or gradlew.bat. Once created, the "gradlew" wrapper is
 * useful for developers who may not have a local gradle installed.
 * 
 * Created during project setup.  Normal usage does not need to
 * re-run this task.  But if you do, we specify a distributionUrl
 * to allow older gradles (<1.0) to invoke this task.
 *
 * http://www.gradle.org/docs/current/userguide/gradle_wrapper.html
 */ 
task wrapper(type: Wrapper) {
    gradleVersion = '1.11'

    // Gradles before v1.0 used a different repo, undesirable now:
    if (distributionUrl.startsWith("http://repo.gradle.org/gradle/distributions/")) {
        distributionUrl = "http://services.gradle.org/distributions/gradle-${gradleVersion}-bin.zip"
    }
}
